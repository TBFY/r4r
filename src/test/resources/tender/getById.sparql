PREFIX org: <http://www.w3.org/ns/org#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ocds: <http://data.tbfy.eu/ontology/ocds#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>
SELECT (?id as ?oid) ?qtitle ?qdescription ?qstatus ?qnumtenderers ?qprocurementmethod ?qprocurementmethoddetails
?qawardcriteria ?qawardcriteriadetails ?qsubmissionmethod ?qsubmissionmethoddetails ?qenquiry ?qeligibilitycriteria
?qtenderperiodstartdate ?qtenderperiodenddate ?qawardperiodstartdate ?qawardperiodenddate ?qminvalueamount ?qminvaluecurrency
?qmaxvalueamount ?qmaxvaluecurrency ?qmildescription ?qmiltitle ?qmilduedate ?qmildocid ?qmildoctype ?qmildoclanguage
?qmildocurl ?qamendmentdate

WHERE {
#Tender information
  ?id a ocds:Tender .
  OPTIONAL { ?id dct:title ?qtitle } .
  OPTIONAL { ?id dct:description ?qdescription } .
  OPTIONAL { ?id ocds:tenderStatus ?qstatus } .
  OPTIONAL { ?id ocds:numberOfTenderers ?qnumtenderers } .
  OPTIONAL { ?id ocds:procurementMethod ?qprocurementmethod .
             ?id ocds:procurementMethodDetails ?qprocurementmethoddetails } .
  OPTIONAL { ?id ocds:awardCriteria ?qawardcriteria .
             ?id ocds:awardCriteriaDetails ?qawardcriteriadetails } .
  OPTIONAL { ?id ocds:submissionMethod ?qsubmissionmethod .
             ?id ocds:submissionMethodDetails ?qsubmissionmethoddetails } .
  OPTIONAL { ?id ocds:enquiry ?qenquiry } .
  OPTIONAL { ?id ocds:eligibilityCriteria ?qeligibilitycriteria } .

#Object Property hasTenderPeriod
  OPTIONAL { ?id ocds:hasTenderPeriod ?htp .
  OPTIONAL { ?htp ocds:startDate ?qtenderperiodstartdate } .
  OPTIONAL { ?htp ocds:endDate ?qtenderperiodenddate }} .

#Object Property hasAwardPeriod
  OPTIONAL { ?id ocds:hasAwardPeriod ?hap .
  OPTIONAL { ?hap ocds:periodStartDate ?qawardperiodstartdate } .
  OPTIONAL { ?hap ocds:periodEndDate ?qawardperiodenddate }} .

#Object Property hasMinEstimatedValue
  OPTIONAL { ?id ocds:hasMinEstimatedValue ?hminev .
  OPTIONAL { ?hminev ocds:valueAmount ?qminvalueamount ;
                     ocds:valueCurrency ?qminvaluecurrency }} .

#Object Property hasMinEstimatedValue
  OPTIONAL { ?id ocds:hasMaxEstimatedValue ?hmaxev .
  OPTIONAL { ?hmaxev ocds:valueAmount ?qmaxvalueamount ;
                     ocds:valueCurrency ?qmaxvaluecurrency }} .

#Object Property hasMilestone
  OPTIONAL { ?id ocds:hasMilestone ?hmil  .
             OPTIONAL { ?hmil dct:description ?qmildescription} .
             OPTIONAL { ?hmil dct:title ?qmiltitle } .
             OPTIONAL { ?hmil ocds:milestoneDueDate ?qmilduedate } .
             OPTIONAL { ?hmil ocds:hasDocument ?hmilhdoc .
                        ?hmilhdoc ocds:documentId ?qmildocid  ;
                                  ocds:documentType ?qmildoctype ;
                                  dct:language ?qmildoclanguage ;
                                  schema:URL ?qmildocurl }} .

#Object Property hasAmendment
  OPTIONAL { ?id ocds:hasAmendment ?hasAmendment .
             ?hasAmendment ocds:amendmentDate ?qamendmentdate } .

#Object Property hasDocument not implemented here, second level resource

#Object Property hasItemToBeProcured not implemented here, second level resource
}
